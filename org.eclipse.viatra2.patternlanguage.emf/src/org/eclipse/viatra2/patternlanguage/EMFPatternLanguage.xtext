/*******************************************************************************
 * Copyright (c) 2011 Zoltan Ujhelyi and Daniel Varro
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *    Zoltan Ujhelyi - initial API and implementation
 *******************************************************************************/
grammar org.eclipse.viatra2.patternlanguage.EMFPatternLanguage with org.eclipse.viatra2.patternlanguage.core.PatternLanguage

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/viatra2/patternlanguage/core/PatternLanguage" as core
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

generate eMFPatternLanguage "http://www.eclipse.org/viatra2/patternlanguage/EMFPatternLanguage"

EMFPatternModel returns core::PatternModel:
	{PatternModel}
	('package' packageName = QualifiedName)?
	importPackages+=PackageImport*
	patterns+=Pattern*;
	
PackageImport:
	'import' ePackage=[ecore::EPackage|STRING]// ('as' alias=ID)?
;

Constraint returns core::Constraint:
	//CheckConstraint	| 
	PatternCompositionConstraint | PathExpressionConstraint // Copied from PatternLanguage
	| EClassConstraint 													  // Additions
;

EClassConstraint:
	type = Type  '(' var = VariableReference ')'
; 

Type returns core::EntityType:
	{ClassType}
	classname=[ecore::EClass]
;

RefType returns core::RelationType:
	{ReferenceType}
	refname=[ecore::EStructuralFeature]
;

ValueReference returns core::ValueReference:
	IntValue | StringValue | VariableValue 
    | DoubleValue | BoolValue | ListValue  //Copied from PatternLanguage 
	| EnumValue							   //Additions
;

EnumValue:
	'::' /*enumeration = [ecore::EEnum] '.' */ literal = [ecore::EEnumLiteral]
;
